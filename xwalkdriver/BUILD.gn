# Copyright 2015 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/ui.gni")
import("//xwalk/xwalk_tests.gni")
import("//testing/test.gni")

# GYP version: xwalk/xwalk_tests.gypi:automation_client_lib
#              (embed_js_in_cpp action)
action("embed_js_in_cpp") {
  script = "embed_js_in_cpp.py"

  js_files = [
    "js/add_cookie.js",
    "js/call_function.js",
    "js/execute_async_script.js",
    "js/focus.js",
    "js/get_element_region.js",
    "js/is_option_element_toggleable.js",
  ]

  inputs = [ "cpp_source.py" ] + js_files

  outputs = [
    "$target_gen_dir/xwalk/js.cc",
    "$target_gen_dir/xwalk/js.h",
  ]
  args = [
    "--directory",
    rebase_path("$target_gen_dir/xwalk", root_build_dir),
  ]
  args += rebase_path(js_files, root_build_dir)
}

# GYP version: xwalk/xwalk_tests.gypi:automation_client_lib
#              (embed_user_data_dir_in_cpp action)
action("embed_user_data_dir_in_cpp") {
  script = "embed_user_data_dir_in_cpp.py"

  files = [
    "xwalk/preferences.txt",
    "xwalk/local_state.txt",
  ]

  inputs = [ "cpp_source.py" ] + files
  outputs = [
    "$target_gen_dir/xwalk/user_data_dir.cc",
    "$target_gen_dir/xwalk/user_data_dir.h",
  ]

  args = [
    "--directory",
    rebase_path("$target_gen_dir/xwalk", root_build_dir),
  ]
  args += rebase_path(files, root_build_dir)
}

# GYP version: xwalk/xwalk_tests.gypi:automation_client_lib
#              (embed_extension_in_cpp action)
action("embed_extension_in_cpp") {
  script = "embed_extension_in_cpp.py"

  files = [
    "extension/background.js",
    "extension/manifest.json",
  ]

  inputs = [ "cpp_source.py" ] + files
  outputs = [
    "$target_gen_dir/xwalk/embedded_automation_extension.cc",
    "$target_gen_dir/xwalk/embedded_automation_extension.h",
  ]

  args = [
    "--directory",
    rebase_path("$target_gen_dir/xwalk", root_build_dir),
  ]
  args += rebase_path(files, root_build_dir)
}

# GYP version: xwalk/xwalk_tests.gypi:automation_client_lib
source_set("automation_client_lib") {
  sources =
      rebase_path(xwalk_tests_gypi_values.xwalk_automation_client_lib_sources,
                  ".",
                  "//xwalk")

  # TODO(jschuh): crbug.com/167187 fix size_t to int truncations.
  configs += [ "//build/config/compiler:no_size_t_to_int_warning" ]

  # Also compile the generated files.
  sources += get_target_outputs(":embed_extension_in_cpp")
  sources += get_target_outputs(":embed_js_in_cpp")
  sources += get_target_outputs(":embed_user_data_dir_in_cpp")

  deps = [
    ":embed_extension_in_cpp",
    ":embed_js_in_cpp",
    ":embed_user_data_dir_in_cpp",
    "//base",
    "//base/third_party/dynamic_annotations",
    "//net",
    "//third_party/zlib:zip",
    "//third_party/zlib:minizip",
    "//ui/accessibility:ax_gen",
    "//ui/base",
    "//ui/gfx",
    "//ui/gfx/geometry",
    "//url",
  ]
}

# GYP version: xwalk/xwalk_tests.gypi:xwalkdriver_lib
#              (embed_version_in_cpp action)
action("embed_version_in_cpp") {
  script = "embed_version_in_cpp.py"
  inputs = [
    "cpp_source.py",
    "VERSION",
  ]
  outputs = [
    "$target_gen_dir/version.cc",
    "$target_gen_dir/version.h",
  ]

  args = [
    "--version-file",
    rebase_path("VERSION", root_build_dir),
    "--directory",
    rebase_path(target_gen_dir, root_build_dir),
  ]
}

# GYP version: xwalk/xwalk_tests.gypi:xwalkdriver_lib
source_set("lib") {
  sources = rebase_path(xwalk_tests_gypi_values.xwalk_driver_lib_sources,
                        ".",
                        "//xwalk")

  # TODO(jschuh): crbug.com/167187 fix size_t to int truncations.
  configs += [ "//build/config/compiler:no_size_t_to_int_warning" ]

  # Also compile the generated version files.
  sources += get_target_outputs(":embed_version_in_cpp")

  # These aren't automatically filtered out.
  if (!use_x11) {
    sources -= [ "keycode_text_conversion_x.cc" ]
  }
  if (!use_ozone) {
    sources -= [ "keycode_text_conversion_ozone.cc" ]
  }

  deps = [
    ":automation_client_lib",
    ":embed_version_in_cpp",
    "//base",
    "//base/third_party/dynamic_annotations",
    "//crypto",
    "//net",
    "//net:http_server",
    "//third_party/zlib",
    "//ui/base",
    "//ui/events:events_base",
    "//ui/gfx",
    "//ui/gfx/geometry",
  ]

  if (use_x11) {
    configs += [ "//build/config/linux:x11" ]
    deps += [ "//ui/gfx/x" ]
  }
}

executable("xwalkdriver") {
  sources = [
    "server/xwalkdriver_server.cc",
  ]

  # TODO(jschuh): crbug.com/167187 fix size_t to int truncations.
  configs += [ "//build/config/compiler:no_size_t_to_int_warning" ]

  deps = [
    ":lib",
  ]
}

test("xwalkdriver_unittests") {
  sources =
      rebase_path(xwalk_tests_gypi_values.xwalk_driver_unittests_sources,
                  ".",
                  "//xwalk")

  # TODO(jschuh): crbug.com/167187 fix size_t to int truncations.
  configs += [ "//build/config/compiler:no_size_t_to_int_warning" ]

  deps = [
    ":lib",
    "//base",
    "//base/test:run_all_unittests",
    "//net",
    "//net:http_server",
    "//testing/gtest",
    "//ui/base",
    "//ui/gfx",
    "//ui/gfx/geometry",
  ]
}

# XwalkDriver tests that aren't run on the main buildbot. Available as an
# optional test type on trybots.
test("xwalkdriver_tests") {
  sources = rebase_path(xwalk_tests_gypi_values.xwalk_driver_tests_sources,
                        ".",
                        "//xwalk")

  # TODO(jschuh): crbug.com/167187 fix size_t to int truncations.
  configs += [ "//build/config/compiler:no_size_t_to_int_warning" ]

  deps = [
    ":lib",
    "//base",
    "//base/test:run_all_unittests",
    "//net",
    "//net:http_server",
    "//net:test_support",
    "//testing/gtest",
    "//url",
  ]
}
